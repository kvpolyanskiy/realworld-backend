buildscript {
    ext {
        springBootVersion = "2.7.6"
        springDependencyManagementVersion = "1.0.15.RELEASE"
        swaggerParserVersion = "2.1.9"
        openApiGeneratorVersion = "6.2.1"
        mapStructVersion = "1.5.3.Final"
        jsonwebtokenVersion = "0.9.1"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id "org.openapi.generator" version "$openApiGeneratorVersion"
}

group = 'kv.polyanskiy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.liquibase:liquibase-core'
    implementation group: "io.jsonwebtoken", name: "jjwt", version: "${jsonwebtokenVersion}"
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    implementation group: "io.swagger.parser.v3", name: "swagger-parser", version: "${swaggerParserVersion}"
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/static/api/swagger/realworld-api.yaml"
    outputDir = "$buildDir/generated/swagger-plugin"
    configFile = "$rootDir/src/main/resources/static/api/swagger/open-api-generator-config.json"
    globalProperties = [
            apis: "",
            models: ""
    ]
}

compileJava.dependsOn tasks.openApiGenerate

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets.main.java.srcDirs += "$buildDir/generated/swagger-plugin"
